#!/usr/bin/env bpftrace
#ifndef BPFTRACE_HAVE_BTF
#include <linux/socket.h>
#include <net/sock.h>
#else
#include <sys/socket.h>
#endif


/*
====PORTMON====
This tool watches TCP connections and reports an all new connections.

When finished, it will provide a county by comman, PID, and destination port.

The objective is to determine anomalous network traffic, erhaps from "benign" applications.
*/

BEGIN
{
  printf("Start monitoring connections by pid/comm at: ");
  time();
}

// With thanks to Brendan Gregg's tcp_connect.bt from bpftrace 
kprobe:tcp_connect
{
  $sk = ((struct sock *) arg0);
  $inet_family = $sk->__sk_common.skc_family;

  if ($inet_family == AF_INET || $inet_family == AF_INET6) {
    if ($inet_family == AF_INET) {
      $daddr = ntop($sk->__sk_common.skc_daddr);
      $saddr = ntop($sk->__sk_common.skc_rcv_saddr);
    } else {
      $daddr = ntop($sk->__sk_common.skc_v6_daddr.in6_u.u6_addr8);
      $saddr = ntop($sk->__sk_common.skc_v6_rcv_saddr.in6_u.u6_addr8);
    }
    $lport = $sk->__sk_common.skc_num;
    $dport = $sk->__sk_common.skc_dport;

    // Destination port is big endian, it must be flipped
    $dport = bswap($dport);

    if (@conns[comm, pid, $dport] > 0) {
      @conns[comm, pid, $dport]++;  
    } else {
      printf("New connection! %s [%d]: %s:%d -> %s:%d\n", comm, pid, $saddr, $lport, $daddr, $dport);
      @conns[comm, pid, $dport] = 1;  
    }

  }
}

END
{
  printf("End monitoring connections by pid/comm at: ");
  time();
}
